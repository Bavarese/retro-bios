;-------------------------------------------------------------------------- 
WRITE_BYTE	PROC	NEAR 
; ЗАПИСЬ БАЙТА НА КАССЕТУ 
; ББАЙТ ДЛЯ ЗАПИСИ ПЕРЕДАЕТСЯ В РЕГИСТРЕ AL. 
;--------------------------------------------------------------------------- 
	PUSH	CX			;СОХРАНИТЬ CX,AX 
	PUSH	AX 
	MOV	CH,AL			;CH <--- ЗАПИСЫВАЕМЫЙ БАЙТ 
					;(СТАРШИЙ БИТ ПИШЕТСЯ ПЕРВЫМ) 
	MOV	CL,8			;ДЛЯ 8 БИТ ДАННЫХ В БАЙТЕ. 
					;ПРИ ЧТЕНИИ ДВА ПЕРЕПАДА СИГНАЛА 
W27:					;ОПРЕДЕЛЯЮТ ОДИН БИТ 
	RCL	CH,1			;CY <--- СТАРШИЙ БИТ 
	PUSHF				;СОНРАНИТЬ ФЛАГИ 
					;ДЛЯ ЗАПИСИ БИТ ПЕРЕДАЕТСЯ В CY 
	CALL	WRITE_BIT		;ЗАПИСАТЬ БИТ ДАННЫХ 
	POPF				;ВОССТАНОВИТЬ CY ДЛЯ ВЫЧИСЛЕНИЯ CRC 
	CALL	CRC_GEN	                ;ВЫЧИСЛИТЬ CRC ДЛЯ ЗАПИСЫВАЕМОГО БИТА 
	DEC	CL			;DEC СЧЕТЧИК БИТ 
	JNZ	W27			;ПОВТОРИТЬ, ЕСЛИ НЕ ВСЕ БИТЫ ЗАПИСАНЫ 
	POP	AX			;ВОССТАНОВИТЬ AX,CX 
	POP	CX 
	RET				;ЗАКОНЧИТЬ ЗАПИСЬ БАЙТА 
WRITE_BYTE	ENDP 
;-------------------------------------------------------------------------- 
WRITE_BIT       PROC    NEAR 
; ЦЕЛЬ: 
; 
; ЗАПИСАТЬ БИТ ДАННЫХ НА КАССЕТУ 
; ЗАПИСЫВАЕМЫЙ БИТ ПЕРЕДАЕТСЯ В CY 
; Т.Е. ЕСЛИ CY=1, ЗАПИСЫВАЕТСЯ БИТ "1", 
;      ЕСЛИ CY=0, ЗАПИСЫВАЕТСЯ БИТ "0" 
; 
; ПРИМЕЧАНИЕ: БИТ ЗАПИСЫВАЕТСЯ ДУМЯ ПЕРЕПАДАМИ СИГНАЛА(1 ПЕРИОД) 
;       БИТ "1" ИМЕЕТ ДЛИТЕЛЬНОСТЬ ПОЛУПЕРИОДА 500 МИКРОСЕКУНД 
;        ( ПЕРИОД - 1000 МИКРОСЕКУНД ) 
; 
;       БИТ "0" ИМЕЕТ ДЛИТЕЛЬНОСТЬ ПОЛУПЕРИОДА 250 МИКРОСЕКУНД 
;        ( ПЕРИОД - 500 МИКРОСЕКУНД ) 
; 
;-------------------------------------------------------------------------- 
                                        ;ПРЕДПОЛОЖИМ, ЧТО '1' 
        MOV     AX,1184                 ;ЗАДАТЬ ПЕРИОД "1" 
        JC      W28                     ;ПЕРЕЙТИ, ЕСЛИ ЗАПИСЫВАЕМИЙ БИТ "1" 
        MOV     AX,592                  ;ИНАЧЕ УСТАНОВИТЬ ПЕРИОД "0" 
W28: 
        PUSH    AX                      ;СОХРАНИТЬ AX 
W29: 
        IN      AL,PORT_C               ;ЧИТАТЬ ВЫХОД КАНАЛА 2 ТАЙМЕРА 
        AND     AL,020H 
        JZ      W29                     ;ЖДАТЬ ПОЯВЛЕНИЕ ВЫСОКОГО УРОВНЯ 
W30: 
        IN      AL,PORT_C               ;ТЕПЕРЬ ЖДАТЬ ПОКА ТАЙМЕР ПЕРЕКЛЮЧИТСЯ 
					;ОБРАТНО В НИЗКИЙ УРОВЕНЬ 
        AND     AL,020H 
        JNZ     W30 
                                        ;ПЕРЕЗАГРУЗИТЬ КАНАЛ 2 ТАЙМЕРА 
                                        ;ДЛЯ ПОЛУЧЕНИЯ ПЕРИОДА СЛЕДУЮЩЕГО БИТА 
        POP     AX                      ;ВОССТАНОВИИТЬ КОЭФФИЦИЕНТ ДЕЛЕНИЯ 
W31:                                    ;ПРОГРАММИРОВАНИЕ ТАЙМЕРА 
        OUT     042H,AL                 ;ЗАПИСАТЬ МЛАДШИЙ БАЙТ В КАНАЛ 2 
        MOV     AL,AH 
        OUT     042H,AL                 ;ЗАПИСАТЬ СТАРШИЙ БАЙТ В КАНАЛ 2 
        RET 
WRITE_BIT       ENDP 
 
;------------------------------------------------------------------------ 
CRC_GEN PROC    NEAR 
; ГЕНЕРИРУЕТ CRC ДЛЯ ЗАПИСЫВАЕМОГО БИТА 
; 
; CRC ИСПОЛЬЗУЕТСЯ ДЛЯ ОБНАРУЖЕНИЯ ОШИБОК ЧТЕНИЯ 
; 
; В CY ПЕРЕДАЕТСЯ БИТ НА КОТОРЫЙ ГЕНЕРИРУЕТСЯ CRC 
; 
; МОДИФИЦИРУЕТ AX И ФЛАГИ 
; 
;------------------------------------------------------------------------- 
        MOV     AX,CRC_REG 
                                        ;ПОСЛЕДУЮЩИЕ ИНСТРУКЦИИ 
                                        ;УСТАНАВЛИВАЮТ ФЛАГ ФЛАГ ПЕРЕПОЛНЕНИЯ, 
                                        ;ЕСЛИ CY И СТАРШИЙ БИТ CRC НЕ РАВНЫ 
        RCR     AX,1 
        RCL     AX,1 
        CLC                             ;ОБНУЛИТЬ CY 
        JNO     W32                     ;ПЕРЕЙТИ, ЕСЛИ ПЕРЕПОНЕНИЕ 
                                        ;ЕСЛИ БИТ ДАННЫХ XOR С 15 РАЗРЯДОМ 
                                        ;CRC РЕГИСТРА = 1, 
        XOR     AX,0810H                ;ТО ВЫПОЛНИТЬ XOR CRC РЕГИСТРА С 
                                        ;0810H 
        STC                             ;УСТАНОВИТЬ CY 
W32: 
        RCL     AX,1                    ;СДВИНУТЬ CY (БИТ ДАННЫХ) 
                                        ;В CRC РЕГИСТР 
        MOV     CRC_REG,AX              ;СОХРАНИТЬ CRC 
        RET                             ;ЗАКОНЧИТЬ 
CRC_GEN ENDP 
 
